buildscript {
  if (project == rootProject) {
    repositories {
      google()
      jcenter()
    }

    dependencies {
        classpath rootProject.ext.has('gradleBuildTools') ? rootProject.ext.get('gradleBuildTools') : 'com.android.tools.build:gradle:3.3.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 28)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 28)
        versionCode 1
        versionName "1.0"
    }

    // Include "lib/" as sources, unfortunetely react-native link can't handle
    // setting up alternative gradle modules. We still have "lib" defined as a
    // standalone gradle module just to be used in AndroidNativeExample
    sourceSets {
        main.java.srcDirs += 'lib/src/main/java'
    }
    
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        if (project == rootProject) {
            maven {
                url "https://dl.bintray.com/wordpress-mobile/react-native-mirror/"
            }
        } else {
            // When building as a dep, the RN's maven repo is locally in the node_modules folder
            def nodeModulesPath = "${project.buildDir}/../../node_modules/"
            maven { url "${nodeModulesPath}/react-native/android" }
        }
    }
}

dependencies {
    if (project == rootProject) {
        // If this is the root project (e.g. Jitpack), specify a version
        implementation 'com.facebook.react:react-native:0.61.5'
    } else {
        //noinspection GradleDynamicVersion
        api "com.facebook.react:react-native:+"
    }
}
